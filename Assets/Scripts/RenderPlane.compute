// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

struct Tile
{
    float height;
    uint owner;
    uint details;
};

RWTexture2D<float4> Result;

RWStructuredBuffer<Tile> Tiles;
RWStructuredBuffer<float3> CountryColours;

int MapMode; // How to display the map

float Resolution; // resolution of heights
float DeepestPoint; // Deepest point in sea
float HighestPoint; // Highest point on land
float LowestPoint; // Lowest point on land
float CountryOpacity; // Opacity of the countries in country map mode

float2 CameraPosition; // -1 to 1 on x and y
float Zoom;


int2 GetCoord(int2 coord, int2 offset)
// Calculates the coord with the offset
{
    int x = coord.x + offset.x;
    int y = coord.y + offset.y;

    x = x % (uint)(Resolution * 2);
    if (x < 0)
        x = (Resolution * 2) + x;

    // 2D map sphere wrapping the y is funny
    if (y < 0)
    {
        y = 0;

        if (x < Resolution)
            x = Resolution + x;
        else
            x = x - Resolution;
    }
    else if (y > Resolution - 1)
    {
        y = Resolution - 1;

        if (x < Resolution)
            x = Resolution + x;
        else
            x = x - Resolution;
    }

    return int2(x, y);
}

uint GetPositionInBuffer(int2 coord)
{
    return coord.x * Resolution + coord.y;
}

// Returns x between 0 and 2 * resolution and y between 0 and resolution
int2 CalculateCoord(uint2 pixel)
{
    uint2 screenResolution = uint2(0, 0);
    Result.GetDimensions(screenResolution.x, screenResolution.y);
    float2 uv = (pixel.xy / (float2) screenResolution) * 2 - 1;
    uv *= Zoom;
    uv += CameraPosition;

    if (uv.x > 1)
        uv.x = -1 + (uv.x - 1);
    if (uv.x < -1)
        uv.x = 1 + (uv.x + 1);
    
    return int2((uv.x + 1) / 2 * Resolution * 2, (uv.y + 1) / 2 * Resolution);
}

[numthreads(1,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    int2 coord = CalculateCoord(id.xy);
    if (coord.y >= Resolution || coord.y < 0)
    {
        Result[id.xy] = float4(0.0, 0.0, 0.0, 1.0);
        return;
    }
    
    uint pos = GetPositionInBuffer(coord);
    float height = Tiles[pos].height;
    uint countryID = Tiles[pos].owner;
    float3 countryColour = CountryColours[countryID];
    bool ocean = false;
    if ((Tiles[pos].details >> 31) % 2 == 1)
        ocean = true;

    float3 terrainColour;
    if (ocean)
        terrainColour = float3(0.0, 0.0, (1 - (height / -DeepestPoint)));
    else
        terrainColour = float3(0.0, sqrt(height / HighestPoint) * 0.8 + 0.2, 0.0);

    switch(MapMode)
    {
        case 0: // Terrain
            Result[id.xy] = float4(terrainColour, 1.0);
            break;

        case 1: // Ocean
            if (ocean)
                Result[id.xy] = float4(1.0, 1.0, 1.0, 1.0);
            else
                Result[id.xy] = float4(0.0, 0.0, 0.0, 1.0);
            break;

        case 2: // Country
            if (countryID == 0)
            {
                Result[id.xy] = float4(terrainColour, 1.0);
                return;
            }

            Result[id.xy] = float4((1 - CountryOpacity) * countryColour + CountryOpacity * terrainColour, 1.0);
            break;

        case 3: //Borders
            if (countryID == 0)
            {
                Result[id.xy] = float4(terrainColour, 1.0);
                return;
            }

            int sameOwnerNeighbours = 0;
            if (Tiles[GetPositionInBuffer(GetCoord(coord, int2(1, 0)))].owner == countryID)
                sameOwnerNeighbours += 1;
            if (Tiles[GetPositionInBuffer(GetCoord(coord, int2(0, 1)))].owner == countryID)
                sameOwnerNeighbours += 1;
            if (Tiles[GetPositionInBuffer(GetCoord(coord, int2(-1, 0)))].owner == countryID)
                sameOwnerNeighbours += 1;
            if (Tiles[GetPositionInBuffer(GetCoord(coord, int2(0, -1)))].owner == countryID)
                sameOwnerNeighbours += 1;

            if (sameOwnerNeighbours < 4)
                Result[id.xy] = float4(countryColour, 1.0);
            else
                Result[id.xy] = float4(terrainColour, 1.0);
            break;
        
        case 4: //Simple Country
            if (ocean)
                Result[id.xy] = float4(terrainColour, 1.0);
            else
                Result[id.xy] = float4(countryColour, 1.0);
            break;
    }
}
